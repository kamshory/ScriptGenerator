<!doctype html>

<title>CodeMirror: Q mode</title>
<meta charset="utf-8"/>
<link rel=stylesheet href="../../doc/docs.css">

<link rel="stylesheet" href="../../lib/codemirror.css">
<script src="../../lib/codemirror.js"></script>
<script src="../../addon/edit/matchbrackets.js"></script>
<script src="q.js"></script>
<style type="text/css">.CodeMirror {border-top: 1px solid black; border-bottom: 1px solid black;}</style>
<div id=nav>
  <a href="http://codemirror.net"><h1>CodeMirror</h1><img id=logo src="../../doc/logo.png"></a>

  <ul>
    <li><a href="../../index.html">Home</a>
    <li><a href="../../doc/manual.html">Manual</a>
    <li><a href="https://github.com/codemirror/codemirror">Code</a>
  </ul>
  <ul>
    <li><a href="../index.html">Language modes</a>
    <li><a class=active href="#">Q</a>
  </ul>
</div>

<article>
<h2>Q mode</h2>


<div><textarea id="code" name="code">
/ utilities to quickly load a csv file - for more exhaustive analysis of the csv contents see csvguess.q
/ 2009.09.20 - updated to match latest csvguess.q 

/ .csv.colhdrs[file] - return a list of colhdrs from file
/ info:.csv.info[file] - return a table of information about the file
/ columns are: 
/	c - column name; ci - column index; t - load type; mw - max width; 
/	dchar - distinct characters in values; rule - rule that caught the type
/	maybe - needs checking, _could_ be say a date, but perhaps just a float?
/ .csv.info0[file;onlycols] - like .csv.info except that it only analyses &lt;onlycols&gt;
/ example:
/	info:.csv.info0[file;(.csv.colhdrs file)like&quot;*price&quot;]
/	info:.csv.infolike[file;&quot;*price&quot;]
/	show delete from info where t=&quot; &quot;
/ .csv.data[file;info] - use the info from .csv.info to read the data
/ .csv.data10[file;info] - like .csv.data but only returns the first 10 rows
/ bulkload[file;info] - bulk loads file into table DATA (which must be already defined :: DATA:() )
/ .csv.read[file]/read10[file] - for when you don't care about checking/tweaking the &lt;info&gt; before reading 

\d .csv
DELIM:&quot;,&quot;
ZAPHDRS:0b / lowercase and remove _ from colhdrs (junk characters are always removed)
WIDTHHDR:25000 / number of characters read to get the header
READLINES:222 / number of lines read and used to guess the types
SYMMAXWIDTH:11 / character columns narrower than this are stored as symbols
SYMMAXGR:10 / max symbol granularity% before we give up and keep as a * string
FORCECHARWIDTH:30 / every field (of any type) with values this wide or more is forced to character &quot;*&quot;
DISCARDEMPTY:0b / completely ignore empty columns if true else set them to &quot;C&quot;
CHUNKSIZE:50000000 / used in fs2 (modified .Q.fs)

k)nameltrim:{$[~@x;.z.s'x;~(*x)in aA:.Q.a,.Q.A;(+/&amp;\~x in aA)_x;x]}
k)fs2:{[f;s]((-7!s)&gt;){[f;s;x]i:1+last@&amp;0xa=r:1:(s;x;CHUNKSIZE);f@`\:i#r;x+i}[f;s]/0j}
cleanhdrs:{{$[ZAPHDRS;lower x except&quot;_&quot;;x]}x where x in DELIM,.Q.an}
cancast:{nw:x$&quot;&quot;;if[not x in&quot;BXCS&quot;;nw:(min 0#;max 0#;::)@\:nw];$[not any nw in x$(11&amp;count y)#y;$[11&lt;count y;not any nw in x$y;1b];0b]}

read:{[file]data[file;info[file]]}  
read10:{[file]data10[file;info[file]]}  

colhdrs:{[file]
	`$nameltrim DELIM vs cleanhdrs first read0(file;0;1+first where 0xa=read1(file;0;WIDTHHDR))}
data:{[file;info]
	(exec c from info where not t=&quot; &quot;)xcol(exec t from info;enlist DELIM)0:file}
data10:{[file;info]
	data[;info](file;0;1+last 11#where 0xa=read1(file;0;15*WIDTHHDR))}
info0:{[file;onlycols]
	colhdrs:`$nameltrim DELIM vs cleanhdrs first head:read0(file;0;1+last where 0xa=read1(file;0;WIDTHHDR));
	loadfmts:(count colhdrs)#&quot;S&quot;;if[count onlycols;loadfmts[where not colhdrs in onlycols]:&quot;C&quot;];
	breaks:where 0xa=read1(file;0;floor(10+READLINES)*WIDTHHDR%count head);
	nas:count as:colhdrs xcol(loadfmts;enlist DELIM)0:(file;0;1+last((1+READLINES)&amp;count breaks)#breaks);
	info:([]c:key flip as;v:value flip as);as:();
	reserved:key`.q;reserved,:.Q.res;reserved,:`i;
	info:update res:c in reserved from info;
	info:update ci:i,t:&quot;?&quot;,ipa:0b,mdot:0,mw:0,rule:0,gr:0,ndv:0,maybe:0b,empty:0b,j10:0b,j12:0b from info;
	info:update ci:`s#ci from info;
	if[count onlycols;info:update t:&quot; &quot;,rule:10 from info where not c in onlycols];
	info:update sdv:{string(distinct x)except`}peach v from info; 
	info:update ndv:count each sdv from info;
	info:update gr:floor 0.5+100*ndv%nas,mw:{max count each x}peach sdv from info where 0&lt;ndv;
	info:update t:&quot;*&quot;,rule:20 from info where mw&gt;.csv.FORCECHARWIDTH; / long values
	info:update t:&quot;C &quot;[.csv.DISCARDEMPTY],rule:30,empty:1b from info where t=&quot;?&quot;,mw=0; / empty columns
	info:update dchar:{asc distinct raze x}peach sdv from info where t=&quot;?&quot;;
	info:update mdot:{max sum each&quot;.&quot;=x}peach sdv from info where t=&quot;?&quot;,{&quot;.&quot;in x}each dchar;
	info:update t:&quot;n&quot;,rule:40 from info where t=&quot;?&quot;,{any x in&quot;0123456789&quot;}each dchar; / vaguely numeric..
	info:update t:&quot;I&quot;,rule:50,ipa:1b from info where t=&quot;n&quot;,mw within 7 15,mdot=3,{all x in&quot;.0123456789&quot;}each dchar,.csv.cancast[&quot;I&quot;]peach sdv; / ip-address
	info:update t:&quot;J&quot;,rule:60 from info where t=&quot;n&quot;,mdot=0,{all x in&quot;+-0123456789&quot;}each dchar,.csv.cancast[&quot;J&quot;]peach sdv;
	info:update t:&quot;I&quot;,rule:70 from info where t=&quot;J&quot;,mw&lt;12,.csv.cancast[&quot;I&quot;]peach sdv;
	info:update t:&quot;H&quot;,rule:80 from info where t=&quot;I&quot;,mw&lt;7,.csv.cancast[&quot;H&quot;]peach sdv;
	info:update t:&quot;F&quot;,rule:90 from info where t=&quot;n&quot;,mdot&lt;2,mw&gt;1,.csv.cancast[&quot;F&quot;]peach sdv;
	info:update t:&quot;E&quot;,rule:100,maybe:1b from info where t=&quot;F&quot;,mw&lt;9;
	info:update t:&quot;M&quot;,rule:110,maybe:1b from info where t in&quot;nIHEF&quot;,mdot&lt;2,mw within 4 7,.csv.cancast[&quot;M&quot;]peach sdv; 
	info:update t:&quot;D&quot;,rule:120,maybe:1b from info where t in&quot;nI&quot;,mdot in 0 2,mw within 6 11,.csv.cancast[&quot;D&quot;]peach sdv; 
	info:update t:&quot;V&quot;,rule:130,maybe:1b from info where t=&quot;I&quot;,mw in 5 6,7&lt;count each dchar,{all x like&quot;*[0-9][0-5][0-9][0-5][0-9]&quot;}peach sdv,.csv.cancast[&quot;V&quot;]peach sdv; / 235959 12345        
	info:update t:&quot;U&quot;,rule:140,maybe:1b from info where t=&quot;H&quot;,mw in 3 4,7&lt;count each dchar,{all x like&quot;*[0-9][0-5][0-9]&quot;}peach sdv,.csv.cancast[&quot;U&quot;]peach sdv; /2359
	info:update t:&quot;U&quot;,rule:150,maybe:0b from info where t=&quot;n&quot;,mw in 4 5,mdot=0,{all x like&quot;*[0-9]:[0-5][0-9]&quot;}peach sdv,.csv.cancast[&quot;U&quot;]peach sdv;
	info:update t:&quot;T&quot;,rule:160,maybe:0b from info where t=&quot;n&quot;,mw within 7 12,mdot&lt;2,{all x like&quot;*[0-9]:[0-5][0-9]:[0-5][0-9]*&quot;}peach sdv,.csv.cancast[&quot;T&quot;]peach sdv;
	info:update t:&quot;V&quot;,rule:170,maybe:0b from info where t=&quot;T&quot;,mw in 7 8,mdot=0,.csv.cancast[&quot;V&quot;]peach sdv;
	info:update t:&quot;T&quot;,rule:180,maybe:1b from info where t in&quot;EF&quot;,mw within 7 10,mdot=1,{all x like&quot;*[0-9][0-5][0-9][0-5][0-9].*&quot;}peach sdv,.csv.cancast[&quot;T&quot;]peach sdv;
	info:update t:&quot;Z&quot;,rule:190,maybe:0b from info where t=&quot;n&quot;,mw within 11 24,mdot&lt;4,.csv.cancast[&quot;Z&quot;]peach sdv;
	info:update t:&quot;P&quot;,rule:200,maybe:1b from info where t=&quot;n&quot;,mw within 12 29,mdot&lt;4,{all x like&quot;[12]*&quot;}peach sdv,.csv.cancast[&quot;P&quot;]peach sdv;
	info:update t:&quot;N&quot;,rule:210,maybe:1b from info where t=&quot;n&quot;,mw within 3 28,mdot=1,.csv.cancast[&quot;N&quot;]peach sdv;
	info:update t:&quot;?&quot;,rule:220,maybe:0b from info where t=&quot;n&quot;; / reset remaining maybe numeric
	info:update t:&quot;C&quot;,rule:230,maybe:0b from info where t=&quot;?&quot;,mw=1; / char
	info:update t:&quot;B&quot;,rule:240,maybe:0b from info where t in&quot;HC&quot;,mw=1,mdot=0,{$[all x in&quot;01tTfFyYnN&quot;;(any&quot;0fFnN&quot;in x)and any&quot;1tTyY&quot;in x;0b]}each dchar; / boolean
	info:update t:&quot;B&quot;,rule:250,maybe:1b from info where t in&quot;HC&quot;,mw=1,mdot=0,{all x in&quot;01tTfFyYnN&quot;}each dchar; / boolean
	info:update t:&quot;X&quot;,rule:260,maybe:0b from info where t=&quot;?&quot;,mw=2,{$[all x in&quot;0123456789abcdefABCDEF&quot;;(any .Q.n in x)and any&quot;abcdefABCDEF&quot;in x;0b]}each dchar; /hex
	info:update t:&quot;S&quot;,rule:270,maybe:1b from info where t=&quot;?&quot;,mw&lt;.csv.SYMMAXWIDTH,mw&gt;1,gr&lt;.csv.SYMMAXGR; / symbols (max width permitting)
	info:update t:&quot;*&quot;,rule:280,maybe:0b from info where t=&quot;?&quot;; / the rest as strings
	/ flag those S/* columns which could be encoded to integers (.Q.j10/x10/j12/x12) to avoid symbols
	info:update j12:1b from info where t in&quot;S*&quot;,mw&lt;13,{all x in .Q.nA}each dchar;
	info:update j10:1b from info where t in&quot;S*&quot;,mw&lt;11,{all x in .Q.b6}each dchar; 
	select c,ci,t,maybe,empty,res,j10,j12,ipa,mw,mdot,rule,gr,ndv,dchar from info}
info:info0[;()] / by default don't restrict columns
infolike:{[file;pattern] info0[file;{x where x like y}[lower colhdrs[file];pattern]]} / .csv.infolike[file;&quot;*time&quot;]

\d .
/ DATA:()
bulkload:{[file;info]
	if[not`DATA in system&quot;v&quot;;'`DATA.not.defined];
	if[count DATA;'`DATA.not.empty];
	loadhdrs:exec c from info where not t=&quot; &quot;;loadfmts:exec t from info;
	.csv.fs2[{[file;loadhdrs;loadfmts] `DATA insert $[count DATA;flip loadhdrs!(loadfmts;.csv.DELIM)0:file;loadhdrs xcol(loadfmts;enlist .csv.DELIM)0:file]}[file;loadhdrs;loadfmts]];
	count DATA}
@[.:;&quot;\\l csvutil.custom.q&quot;;::]; / save your custom settings in csvutil.custom.q to override those set at the beginning of the file 
</textarea></div>

    <script>
      var editor = CodeMirror.fromTextArea(document.getElementById("code"), {
        lineNumbers: true,
        matchBrackets: true
      });
    </script>

    <p><strong>MIME type defined:</strong> <code>text/x-q</code>.</p>
  </article>
